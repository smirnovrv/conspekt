
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)	

	ДобавитьСтрокуКомментарии(ВыбранноеЗначение);
	
	Объект.ОсновнойТекст = СформироватьОсновнойТекст();
	
	Модифицированность = Истина;
КонецПроцедуры

 &НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
 	Если ПолучитьКоличествоСтрокКомментариев() = 0 Тогда
 		ДобавитьНачальнуюСтраницуHTML();	
 	КонецЕсли;	
 	
 	Объект.ОсновнойТекст = СформироватьОсновнойТекст();
 КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОсновнойТекстПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	ПоискЯкоря = СтрНайти(ДанныеСобытия.Element.outerHTML, "#");
	Если ПоискЯкоря > 0 Тогда
		Попытка
			NameID 		= ДанныеСобытия.Element.innerText;
			ТекID 		= Элементы.ОсновнойТекст.Документ.getElementById(NameID);
			ТекПозиция 	= ТекID.offsetTop;
			Элементы.ОсновнойТекст.Документ.body.scrollTop = ТекПозиция;
		Исключение
			ПоискЯкоря = 0;
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область  ОбработчикиСобытийЭлементовТаблицыФормыКомментарии
&НаКлиенте
Процедура КомментарииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если Не Поле.Имя = "КомментарииКартинка" Тогда
		СтрутураПараметров = Новый Структура;
		ТекСтрока = Элементы.Комментарии.ТекущиеДанные;
		СтрутураПараметров.Вставить("Комментарий", 		текСтрока.Комментарий);
		СтрутураПараметров.Вставить("Тэг", 				текСтрока.Тэг);
		СтрутураПараметров.Вставить("НомерСтроки",		текСтрока.НомерСтроки);	
		СтрутураПараметров.Вставить("Якорь",			текСтрока.Якорь);  	
		СтрутураПараметров.Вставить("Вставка",			Ложь);  	
		
		ОткрытьФорму("Справочник.Ленты.Форма.ФормаРедактирования", СтрутураПараметров, ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

 #Область ОбработчикиКомандФормы
&НаКлиенте
Процедура КомандаВвестиТекст(Команда)
	СтрутураПараметров = Новый Структура;	
	
	ТекСтрока = Элементы.Комментарии.ТекущиеДанные;
		
	Если ТекСтрока = Неопределено Тогда
		НомерСтроки = ПолучитьКоличествоСтрокКомментариев();
	Иначе	
		НомерСтроки = текСтрока.НомерСтроки;
	КонецЕсли;
	
	СтрутураПараметров.Вставить("НомерСтроки",		НомерСтроки);
	СтрутураПараметров.Вставить("Вставка",			Истина);
	
	ОткрытьФорму("Справочник.Ленты.Форма.ФормаРедактирования", СтрутураПараметров, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайлНовыйВариант(Команда)
	Фильтр = "Все картинки (*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf)|*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf|" 
	+ "Формат bmp (*.bmp;*.dib;*.rle)|*.bmp;*.dib;*.rle|"
	+ "Формат JPEG (*.jpg;*.jpeg)|*.jpg;*.jpeg|"
	+ "Формат TIFF (*.tif)|*.tif|"
	+ "Формат GIF (*.gif)|*.gif|"
	+ "Формат PNG (*.png)|*.png|"
	+ "Формат icon (*.ico)|*.ico|"
	+ "Формат метафайл (*.wmf;*.emf)|*.wmf;*.emf|";
	
	Диалог = новый ПараметрыДиалогаПомещенияФайлов("Выберите файл картинки", Ложь, Фильтр);
		
	Оповещение = новый ОписаниеОповещения("ПослеЗакрытияДиалогаВыбора", ЭтотОбъект);
	
	НачатьПомещениеФайлаНаСервер(Оповещение,,,, Диалог, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНумерованныйСписок(Команда)

	ДобавитьСписокHTML("<ol>");

КонецПроцедуры
	
&НаКлиенте
Процедура ДобавитьМаркированныйСписок(Команда)
	ДобавитьСписокHTML("<ul>");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавитьСписокHTML(Вид)
	ТекСтрока = Элементы.Комментарии.ТекущиеДанные;
		
	Если ТекСтрока = Неопределено Тогда
		ИсходныйНомерСтроки = ПолучитьКоличествоСтрокКомментариев();
	Иначе	
		ИсходныйНомерСтроки = текСтрока.ИсходныйНомерСтроки;
	КонецЕсли;
	
	НоваяСтрока = Объект.Комментарии.Вставить(ИсходныйНомерСтроки-1);
	НоваяСтрока.Комментарий = Вид;
	
	НоваяСтрока = Объект.Комментарии.Вставить(ИсходныйНомерСтроки);
	Вид2 = Лев(Вид, 1) + "/" + Прав(вид, 3); 
	НоваяСтрока.Комментарий = Вид2;
	
	Модифицированность = Истина;
КонецПроцедуры

 &НаСервере
Функция СформироватьОсновнойТекст()
	ОсновнойТекст = "";
	
	ТекстСсылок = СформироватьМенюСсылок();
	
	Для Каждого СтрокаТЧ Из Объект.Комментарии Цикл
		Если СтрокаТЧ.НомерСтроки = 2 Тогда
			ОсновнойТекст = ОсновнойТекст + ТекстСсылок;
		КонецЕсли;	
		Если ЗначениеЗаполнено(СтрокаТЧ.Тэг) Тогда
			ТекстДляВставки = "";
			Если СтрокаТЧ.Тэг.Одиночный Тогда			
				ТекстДляВставки = ПолучитьТекстДляВставки_Одиночный(СтрокаТЧ);
			Иначе
				ТекстДляВставки = ПолучитьТекстДляВставки_Двойной(СтрокаТЧ);
			КонецЕсли;
			ОсновнойТекст = ОсновнойТекст + ТекстДляВставки;
		Иначе //Начало и конец HTML и BODY
			ОсновнойТекст = ОсновнойТекст + СтрокаТЧ.Комментарий;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОсновнойТекст;
	
КонецФункции

&НаСервере
Функция ПолучитьТекстДляВставки_Двойной(СтрокаТЧ)
	Если ЗначениеЗаполнено(СтрокаТЧ.Якорь) Тогда
		ТекстДляВставки = ПолучитьТекстдляВставки_Якорь(СтрокаТЧ);	
	Иначе
		ТекстДляВставки = ПолучитьТекстДляВставки(СтрокаТЧ);
	КонецЕсли;
	
	Возврат ТекстДляВставки;
КонецФункции

&НаСервере
Функция ПолучитьТекстДляВставки(СтрокаТЧ)
	ТекстДляВставки = "";
	
	ТекстВставки = СтрЗаменить(СтрокаТЧ.Комментарий,Символы.ПС ,"<br>");
	ТекстДляВставки =  
		"<" + СтрокаТЧ.Тэг.Наименование 
		+ " style = 'margin-left: 40px';>"
		+ ТекстВставки 
		+ "</" 
		+ СтрокаТЧ.Тэг.Наименование 
		+ ">";
	
	Возврат ТекстДляВставки
	
КонецФункции

&НаСервере
Функция ПолучитьТекстдляВставки_Якорь(СтрокаТЧ)
	Если СтрокаТЧ.Тэг = Справочники.Тэги.a Тогда
		ТекстДляВставки = "<" + СтрокаТЧ.Тэг.Наименование + " href=""" + СтрокаТЧ.Якорь + """>"
			+ СтрокаТЧ.Комментарий + "</" + СтрокаТЧ.Тэг.Наименование + ">";
	Иначе
		ТекстДляВставки = "<" + СтрокаТЧ.Тэг.Наименование + " id=""" + СтрокаТЧ.Якорь + """>"
			+ СтрокаТЧ.Комментарий + "</" + СтрокаТЧ.Тэг.Наименование + ">";
	КонецЕсли;
	
	Возврат ТекстДляВставки;
	
КонецФункции

&НаСервере
Функция ПолучитьТекстДляВставки_Одиночный(СтрокаТЧ)
	Если ЗначениеЗаполнено(СтрокаТЧ.Картинка) Тогда					
		ТекстДляВставки = ПолучитьТекстДляВставки_Картинки(СтрокаТЧ);
	Иначе
		ТекстДляВставки = "<" + СтрокаТЧ.Тэг.Наименование + ">" + СтрокаТЧ.Комментарий; //не картинка
	КонецЕсли;
	
	Возврат ТекстДляВставки;
КонецФункции

 &НаСервере
Функция ПолучитьТекстВставкиСРазмерами(СтруктураСвойств)
 	
 	Высота = ?(СтруктураСвойств.Высота = 0, 120, СтруктураСвойств.Высота);
	Ширина = ?(СтруктураСвойств.Ширина = 0, 150, СтруктураСвойств.Ширина);
 	
 	ТекстВставки = СтрЗаменить(СтруктураСвойств.ТекстДляВставки, ">", " 'width=" + Ширина + "px height=" + Высота +"px'>");
 	
 	СтруктураСвойств.Вставить("ТекстДляВставки", ТекстВставки);
 	 	
 	Возврат СтруктураСвойств;
КонецФункции
 		
 &НаСервере
Функция ПолучитьТекстДляВставки_Картинки(СтрокаТЧ)
	
	//1-й способ вывести картинку
	//ТекКартинка = ПолучитьНавигационнуюСсылку(СтрокаТЧ.Картинка, "ФайлХранилище");
	//
	//2-й способ вывести картинку
	//			на клиенте			
	//Макет = ПолучитьМакет("Макет");	
	//Данные = Макет.Области.Dogs.Картинка;
	//ИмяФайла = "5.jpg";
	//			на сервере
	//ПолныйПуть = КаталогВременныхФайлов() + ИмяФайла;
	//			на клиенте
	//Данные.Записать(ПолныйПуть);
	//Текст = ПолучитьМакет("HTML");										
	//ПолеHTML = СтрЗаменить(Текст.ПолучитьТекст(), ИмяФайла, ПолныйПуть);
					
	//3-й способ вывести картинку					
	ТекстДляВставки = "";
					//@skip-check reading-attribute-from-database
	ДвоичныеДанныеФотографии = СтрокаТЧ.Картинка.ФайлХранилище.Получить();
	Base64ДанныеФотографии = Base64Строка(ДвоичныеДанныеФотографии);

	ТекстДляВставки = "<p></p>";
	ТекстДляВставки = ТекстДляВставки + "<img src='data:image/;base64," + Base64ДанныеФотографии + "'>";

	СтруктураСвойств = Новый Структура;
	СтруктураСвойств.Вставить("ТекстДляВставки", ТекстДляВставки);
	СтруктураСвойств.Вставить("Высота", СтрокаТЧ.Высота);
	СтруктураСвойств.Вставить("Ширина", СтрокаТЧ.Ширина);	
					
	//Пока без ограничения исходного размера
	//ТекстДляВставки = ТекстДляВставки + ПолучитьТекстВставкиСРазмерами(СтруктураСвойств).ТекстДляВставки;

	ТекстДляВставки = СтрЗаменить(ТекстДляВставки, "'>", "'  style = 'margin-left: 40px'>");
	Возврат ТекстДляВставки;
КонецФункции
	
 &НаСервере
 Функция СформироватьМенюСсылок()
 	ТекстДляВставки = "";
 	Для Каждого СтрокаТЧ Из Объект.Комментарии Цикл
 			Если ЗначениеЗаполнено(СтрокаТЧ.Якорь) Тогда
 				Если СтрокаТЧ.Тэг = Справочники.Тэги.H1 Тогда
 					ТекстДляВставки = ТекстДляВставки 
 						+ "<p style = 'margin-left: 10px';><a href=""#"+ СтрокаТЧ.Якорь + """>" + СтрокаТЧ.Якорь + "</a></p>";
 				ИначеЕсли СтрокаТЧ.Тэг = Справочники.Тэги.H2 Тогда
 					ТекстДляВставки = ТекстДляВставки 
 						+ "<p style = 'margin-left: 20px';><a href=""#" + СтрокаТЧ.Якорь + """>" + СтрокаТЧ.Якорь + "</a></p>";
 				ИначеЕсли СтрокаТЧ.Тэг = Справочники.Тэги.H3 Тогда
 					ТекстДляВставки = ТекстДляВставки 
 						+ "<p style = 'margin-left: 30px';><a href=""#" + СтрокаТЧ.Якорь + """>" + СтрокаТЧ.Якорь + "</a></p>";	
 				ИначеЕсли СтрокаТЧ.Тэг = Справочники.Тэги.H4 Тогда
 					ТекстДляВставки = ТекстДляВставки 
 						+ "<p style = 'margin-left: 40px';><a href=""#" + СтрокаТЧ.Якорь + """>" + СтрокаТЧ.Якорь + "</a></p>";	
 				ИначеЕсли СтрокаТЧ.Тэг = Справочники.Тэги.H5 Тогда
 					ТекстДляВставки = ТекстДляВставки 
 						+ "<p style = 'margin-left: 50px';><a href=""#" + СтрокаТЧ.Якорь + """>" + СтрокаТЧ.Якорь + "</a></p>";
 				ИначеЕсли СтрокаТЧ.Тэг = Справочники.Тэги.H6 Тогда
 					ТекстДляВставки = ТекстДляВставки 
 						+ "<p style = 'margin-left: 60px';><a href=""#" + СтрокаТЧ.Якорь + """>" + СтрокаТЧ.Якорь + "</a></p>";												
 				Иначе	
// 					ТекстДляВставки = ТекстДляВставки 
// 						+ "<p><a href=""#" + СтрокаТЧ.Якорь + """>" + СтрокаТЧ.Якорь + "</a></p>";
 				КонецЕсли;
 			КонецЕсли;	
 	КонецЦикла;	
 	
 	Возврат ТекстДляВставки;
 КонецФункции

 &НаСервере
 Процедура ДобавитьНачальнуюСтраницуHTML()
 	ДобавитьНачалоHTML();	
 	ДобавитьКонецHTML();
 КонецПроцедуры

 &НаСервере
 Процедура ДобавитьКонецHTML()
 	НоваяСтрока = Объект.Комментарии.Добавить();
 	
 	ТекстКоментария =	"	</body>";
 	ТекстКоментария = ТекстКоментария + Символы.ПС + "</html>";
 	 
 	НоваяСтрока.Комментарий = ТекстКоментария;
 КонецПроцедуры

 &НаСервере
 Процедура ДобавитьНачалоHTML()
 	НоваяСтрока = Объект.Комментарии.Добавить();
 	
 	ТекстКоментария = "<!DOCTYPE html>"; 
 	ТекстКоментария = ТекстКоментария + Символы.ПС + "<html lang=""ru""" + ">";
 	ТекстКоментария = ДобавитьHead(ТекстКоментария);
 	ТекстКоментария = ТекстКоментария + Символы.ПС + "	<body>";
 	
 	НоваяСтрока.Комментарий = ТекстКоментария; 
 КонецПроцедуры
 
 &НаСервере
 Функция ДобавитьHead(ТекстКоментария)
 	ТекстКоментария = ТекстКоментария + Символы.ПС + "	<head>";
 	ТекстКоментария = ТекстКоментария + Символы.ПС + "		<meta charset=""UTF-8""" + ">";
 	ТекстКоментария = ТекстКоментария + Символы.ПС + "		 <title>" + Объект.Наименование + "</title>";
 	ТекстКоментария = ТекстКоментария + Символы.ПС + "	</head>";
 	
 	Возврат ТекстКоментария; 
 КонецФункции

&НаКлиенте
Процедура ОбработатьВыборФайла(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат; 
	КонецЕсли;
	
	ОписаниеФайла = Новый Файл(ВыбранноеИмяФайла);
	
	СсылкаНаОбъект = Объект.Ссылка; 
	СохранитьФайлВХранилище(Адрес, СсылкаНаОбъект, ОписаниеФайла.ИмяБезРасширения);	
 	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияДиалогаВыбора (ОписаниеФайла, ДопПараметры) Экспорт
	
	Если ОписаниеФайла.ПомещениеФайлаОтменено Тогда
		Возврат;
	КОнецЕсли;
	
	СсылкаНаОбъект = Объект.Ссылка; 
	СсылкаФайл = СохранитьФайлВХранилище(ОписаниеФайла.Адрес, СсылкаНаОбъект, ОписаниеФайла.СсылкаНаФайл.Файл.ИмяБезРасширения);	
	ИсходныйНомерСтроки = Элементы.Комментарии.ТекущиеДанные.ИсходныйНомерСтроки;
	НоваяСтрока = Объект.Комментарии.Вставить(ИсходныйНомерСтроки);
	НоваяСтрока.Картинка = СсылкаФайл;
	НоваяСтрока.Тэг = ПолучитьТэгКартинки();
	
	Объект.Комментарии.Сдвинуть(ПолучитьКоличествоСтрокКомментариев() - 1, -1);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТэгКартинки()	
	Возврат Справочники.Тэги.img;	
КонецФункции

&НаСервере
Функция ПолучитьКоличествоСтрокКомментариев()
	Возврат Объект.Комментарии.Количество();
КонецФункции

&НаСервере
Функция СохранитьФайлВХранилище(Адрес, Владелец, Имя)

	НовФайл = Справочники.ЛентыФайлы.СоздатьЭлемент();
	НовФайл.ВладелецФайла 		= Владелец;
	НовФайл.Наименование 		= Имя;
	НовФайл.ДатаСоздания 		= ТекущаяДатаСеанса();
	НовФайл.ФайлХранилище 		= Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Адрес));
	НовФайл.Записать();
	
	Возврат НовФайл.Ссылка;
КонецФункции
 
&НаКлиенте
Процедура ДобавитьСтрокуКомментарии(ВыбранноеЗначение)

	Если ВыбранноеЗначение.Вставка Тогда
		НоваяСтрока = Объект.Комментарии.Вставить(ВыбранноеЗначение.НомерСтроки);
	Иначе
		НомерСтроки = ВыбранноеЗначение.НомерСтроки;
		Если НомерСтроки > 0 Тогда
			НоваяСтрока = Элементы.Комментарии.ТекущиеДанные;
		Иначе
			НоваяСтрока = Объект.Комментарии.Добавить();
		КонецЕсли;
	КонецЕсли;

	НоваяСтрока.Комментарий = ВыбранноеЗначение.ВведенныйТекст;
	НоваяСтрока.Тэг 		= ВыбранноеЗначение.Тэг;
	НоваяСтрока.Якорь 		= ВыбранноеЗначение.Якорь;
	
	Если НомерСтроки = 0 Тогда
		Объект.Комментарии.Сдвинуть(ПолучитьКоличествоСтрокКомментариев() - 1, -1);
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти