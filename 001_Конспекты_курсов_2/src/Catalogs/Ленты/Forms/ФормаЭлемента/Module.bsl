
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)	

	ДобавитьСтрокуКомментарии(ВыбранноеЗначение);
	
	СформироватьОсновнойТекст();
	
	Модифицированность = Истина;
КонецПроцедуры

 &НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
 	Если ПолучитьКоличествоСтрокКомментариев() = 0 Тогда
 		ДобавитьНачальнуюСтраницуHTML();	
 	КонецЕсли;	
 	
 	СформироватьОсновнойТекст();
 КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОсновнойТекстПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	ПоискЯкоря = СтрНайти(ДанныеСобытия.Element.outerHTML, "#");
	Если ПоискЯкоря > 0 Тогда
		NameID 		= ДанныеСобытия.Element.innerText;
		ТекID 		= Элементы.ОсновнойТекст.Документ.getElementById(NameID);
		ТекПозиция 	= ТекID.offsetTop;
		Элементы.ОсновнойТекст.Документ.body.scrollTop = ТекПозиция;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область  ОбработчикиСобытийЭлементовТаблицыФормыКомментарии
&НаКлиенте
Процедура КомментарииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если Не Поле.Имя = "КомментарииКартинка" Тогда
		СтрутураПараметров = Новый Структура;
		ТекСтрока = Элементы.Комментарии.ТекущиеДанные;
		СтрутураПараметров.Вставить("Комментарий", 		текСтрока.Комментарий);
		СтрутураПараметров.Вставить("Тэг", 				текСтрока.Тэг);
		СтрутураПараметров.Вставить("НомерСтроки",		текСтрока.НомерСтроки);	
		СтрутураПараметров.Вставить("Якорь",			текСтрока.Якорь);  	
		СтрутураПараметров.Вставить("Вставка",			Ложь);  	
		
		ОткрытьФорму("Справочник.Ленты.Форма.ФормаРедактирования", СтрутураПараметров, ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

 #Область ОбработчикиКомандФормы
&НаКлиенте
Процедура КомандаВвестиТекст(Команда)
	СтрутураПараметров = Новый Структура;	
	
	ТекСтрока = Элементы.Комментарии.ТекущиеДанные;
		
	Если ТекСтрока = Неопределено Тогда
		НомерСтроки = ПолучитьКоличествоСтрокКомментариев();
	Иначе	
		НомерСтроки = текСтрока.НомерСтроки;
	КонецЕсли;
	
	СтрутураПараметров.Вставить("НомерСтроки",		НомерСтроки);
	СтрутураПараметров.Вставить("Вставка",			Истина);
	
	ОткрытьФорму("Справочник.Ленты.Форма.ФормаРедактирования", СтрутураПараметров, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайл(Команда)
	ДобавитьФайлы();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайлНовыйВариант(Команда)
	Фильтр = "Все картинки (*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf)|*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf|" 
	+ "Формат bmp (*.bmp;*.dib;*.rle)|*.bmp;*.dib;*.rle|"
	+ "Формат JPEG (*.jpg;*.jpeg)|*.jpg;*.jpeg|"
	+ "Формат TIFF (*.tif)|*.tif|"
	+ "Формат GIF (*.gif)|*.gif|"
	+ "Формат PNG (*.png)|*.png|"
	+ "Формат icon (*.ico)|*.ico|"
	+ "Формат метафайл (*.wmf;*.emf)|*.wmf;*.emf|";
	
	Диалог = новый ПараметрыДиалогаПомещенияФайлов("Выберите файл картинки", Ложь, Фильтр);
		
	Оповещение = новый ОписаниеОповещения("ПослеЗакрытияДиалогаВыбора", ЭтотОбъект);
	
	НачатьПомещениеФайлаНаСервер(Оповещение,,,, Диалог, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНумерованныйСписок(Команда)
	
	ТекСтрока = Элементы.Комментарии.ТекущиеДанные;
		
	Если ТекСтрока = Неопределено Тогда
		ИсходныйНомерСтроки = ПолучитьКоличествоСтрокКомментариев();
	Иначе	
		ИсходныйНомерСтроки = текСтрока.ИсходныйНомерСтроки;
	КонецЕсли;
	
	НоваяСтрока = Объект.Комментарии.Вставить(ИсходныйНомерСтроки-1);
	НоваяСтрока.Комментарий = "<ol>";
	
	НоваяСтрока = Объект.Комментарии.Вставить(ИсходныйНомерСтроки);
	НоваяСтрока.Комментарий = "</ol>";
	
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
 &НаСервере
Процедура СформироватьОсновнойТекст()
	Объект.ОсновнойТекст = "";
	
	ТекстСсылок = СформироватьМенюСсылок();
	
	Для Каждого СтрокаТЧ Из Объект.Комментарии Цикл
		Если СтрокаТЧ.НомерСтроки = 2 Тогда
			Объект.ОсновнойТекст = Объект.ОсновнойТекст + ТекстСсылок;
		КонецЕсли;	
		Если ЗначениеЗаполнено(СтрокаТЧ.Тэг) Тогда
			//@skip-check reading-attribute-from-database
			Если СтрокаТЧ.Тэг.Одиночный Тогда
				Если ЗначениеЗаполнено(СтрокаТЧ.Картинка) Тогда
					ТекКартинка = ПолучитьНавигационнуюСсылку(СтрокаТЧ.Картинка, "ФайлХранилище");
					Если ЗначениеЗаполнено(СтрокаТЧ.Комментарий) Тогда
						ТекстДляВставки = "<img src=" + ТекКартинка + " width=""50"" height=""50"" >";
					Иначе
						Высота = ?(СтрокаТЧ.Высота = 0, 300, СтрокаТЧ.Высота);
						Ширина = ?(СтрокаТЧ.Ширина = 0, 400, СтрокаТЧ.Ширина);
						ТекстДляВставки = "<p></p>" + "<img src=" + ТекКартинка + " width=" + Ширина + "height="
							+ Высота + " >";
					КонецЕсли;
				Иначе
					ТекстДляВставки = "<" + СтрокаТЧ.Тэг.Наименование + ">" + СтрокаТЧ.Комментарий;
				КонецЕсли;
			Иначе
				Если ЗначениеЗаполнено(СтрокаТЧ.Якорь) Тогда
					ТекстДляВставки = "<" + СтрокаТЧ.Тэг.Наименование + " id=""" + СтрокаТЧ.Якорь + """>"
						+ СтрокаТЧ.Комментарий + "</" + СтрокаТЧ.Тэг.Наименование + ">";
				Иначе
					ТекстДляВставки = "<" + СтрокаТЧ.Тэг.Наименование + " style = 'margin-left: 40px';>" + СтрокаТЧ.Комментарий + "</"
						+ СтрокаТЧ.Тэг.Наименование + ">";
				КонецЕсли;
			КонецЕсли;
			ТекстДляВставки = СтрЗаменить(ТекстДляВставки, Символы.ПС, "<br>");
			Объект.ОсновнойТекст = Объект.ОсновнойТекст + ТекстДляВставки;
		Иначе
			Объект.ОсновнойТекст = Объект.ОсновнойТекст + СтрокаТЧ.Комментарий;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

 &НаСервере
 Функция СформироватьМенюСсылок()
 	ТекстДляВставки = "";
 	Для Каждого СтрокаТЧ Из Объект.Комментарии Цикл
 			Если ЗначениеЗаполнено(СтрокаТЧ.Якорь) Тогда
 				Если СтрокаТЧ.Тэг.Наименование = "h1" Тогда
 					ТекстДляВставки = ТекстДляВставки 
 						+ "<p style = 'margin-left: 10px';><a href=""#"+ СтрокаТЧ.Якорь + """>" + СтрокаТЧ.Якорь + "</a></p>";
 				ИначеЕсли СтрокаТЧ.Тэг.Наименование = "h2" Тогда
 					ТекстДляВставки = ТекстДляВставки 
 						+ "<p style = 'margin-left: 20px';><a href=""#" + СтрокаТЧ.Якорь + """>" + СтрокаТЧ.Якорь + "</a></p>";
 				ИначеЕсли СтрокаТЧ.Тэг.Наименование = "h3" Тогда
 					ТекстДляВставки = ТекстДляВставки 
 						+ "<p style = 'margin-left: 30px';><a href=""#" + СтрокаТЧ.Якорь + """>" + СтрокаТЧ.Якорь + "</a></p>";	
 				ИначеЕсли СтрокаТЧ.Тэг.Наименование = "h4" Тогда
 					ТекстДляВставки = ТекстДляВставки 
 						+ "<p style = 'margin-left: 40px';><a href=""#" + СтрокаТЧ.Якорь + """>" + СтрокаТЧ.Якорь + "</a></p>";	
 				ИначеЕсли СтрокаТЧ.Тэг.Наименование = "h5" Тогда
 					ТекстДляВставки = ТекстДляВставки 
 						+ "<p style = 'margin-left: 50px';><a href=""#" + СтрокаТЧ.Якорь + """>" + СтрокаТЧ.Якорь + "</a></p>";
 				ИначеЕсли СтрокаТЧ.Тэг.Наименование = "h6" Тогда
 					ТекстДляВставки = ТекстДляВставки 
 						+ "<p style = 'margin-left: 60px';><a href=""#" + СтрокаТЧ.Якорь + """>" + СтрокаТЧ.Якорь + "</a></p>";												
 				Иначе	
 					ТекстДляВставки = ТекстДляВставки 
 						+ "<p><a href=""#" + СтрокаТЧ.Якорь + """>" + СтрокаТЧ.Якорь + "</a></p>";
 				КонецЕсли;
 			КонецЕсли;	
 	КонецЦикла;	
 	
 	Возврат ТекстДляВставки;
 КонецФункции

 &НаСервере
 Процедура ДобавитьНачальнуюСтраницуHTML()
 	ДобавитьНачалоHTML();	
 	ДобавитьКонецHTML();
 КонецПроцедуры

 &НаСервере
 Процедура ДобавитьКонецHTML()
 	НоваяСтрока = Объект.Комментарии.Добавить();
 	
 	ТекстКоментария =	"	</body>";
 	ТекстКоментария = ТекстКоментария + Символы.ПС + "</html>";
 	 
 	НоваяСтрока.Комментарий = ТекстКоментария;
 КонецПроцедуры

 &НаСервере
 Процедура ДобавитьНачалоHTML()
 	НоваяСтрока = Объект.Комментарии.Добавить();
 	
 	ТекстКоментария = "<!DOCTYPE html>"; 
 	ТекстКоментария = ТекстКоментария + Символы.ПС + "<html lang=""ru""" + ">";
 	ТекстКоментария = ДобавитьHead(ТекстКоментария);
 	ТекстКоментария = ТекстКоментария + Символы.ПС + "	<body>";
 	
 	НоваяСтрока.Комментарий = ТекстКоментария; 
 КонецПроцедуры
 
 &НаСервере
 Функция ДобавитьHead(ТекстКоментария)
 	ТекстКоментария = ТекстКоментария + Символы.ПС + "	<head>";
 	ТекстКоментария = ТекстКоментария + Символы.ПС + "		<meta charset=""UTF-8""" + ">";
 	ТекстКоментария = ТекстКоментария + Символы.ПС + "		 <title>" + Объект.Наименование + "</title>";
 	ТекстКоментария = ТекстКоментария + Символы.ПС + "	</head>";
 	
 	Возврат ТекстКоментария; 
 КонецФункции
 
 &НаКлиенте
Процедура ДобавитьФайлы()
	УИД 		= Новый УникальныйИдентификатор();
	Оповещение  = Новый ОписаниеОповещения("ОбработатьВыборФайла",   ЭтотОбъект);
	НачатьПомещениеФайла(Оповещение,   ,   ,   Истина,   УИД);
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборФайла(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат; 
	КонецЕсли;
	
	ОписаниеФайла = Новый Файл(ВыбранноеИмяФайла);
	
	СсылкаНаОбъект = Объект.Ссылка; 
	СохранитьФайлВХранилище(Адрес, СсылкаНаОбъект, ОписаниеФайла.ИмяБезРасширения);	
 	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияДиалогаВыбора (ОписаниеФайла, ДопПараметры) Экспорт
	
	Если ОписаниеФайла.ПомещениеФайлаОтменено Тогда
		Возврат;
	КОнецЕсли;
	
	СсылкаНаОбъект = Объект.Ссылка; 
	СсылкаФайл = СохранитьФайлВХранилище(ОписаниеФайла.Адрес, СсылкаНаОбъект, ОписаниеФайла.СсылкаНаФайл.Файл.ИмяБезРасширения);	
	ИсходныйНомерСтроки = Элементы.Комментарии.ТекущиеДанные.ИсходныйНомерСтроки;
	НоваяСтрока = Объект.Комментарии.Вставить(ИсходныйНомерСтроки);
	НоваяСтрока.Картинка = СсылкаФайл;
	НоваяСтрока.Тэг = ПолучитьТэгКартинки();
	
	Объект.Комментарии.Сдвинуть(ПолучитьКоличествоСтрокКомментариев() - 1, -1);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТэгКартинки()	
	Возврат Справочники.Тэги.НайтиПоНаименованию("img");	
КонецФункции

&НаСервере
Функция ПолучитьКоличествоСтрокКомментариев()
	Возврат Объект.Комментарии.Количество();
КонецФункции

&НаСервере
Функция СохранитьФайлВХранилище(Адрес, Владелец, Имя)

	НовФайл = Справочники.ЛентыФайлы.СоздатьЭлемент();
	НовФайл.ВладелецФайла 		= Владелец;
	НовФайл.Наименование 		= Имя;
	НовФайл.ДатаСоздания 		= ТекущаяДатаСеанса();
	НовФайл.ФайлХранилище 		= Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Адрес));
	НовФайл.Записать();
	
	Возврат НовФайл.Ссылка;
КонецФункции
 
&НаКлиенте
Процедура ДобавитьСтрокуКомментарии(ВыбранноеЗначение)

	Если ВыбранноеЗначение.Вставка Тогда
		НоваяСтрока = Объект.Комментарии.Вставить(ВыбранноеЗначение.НомерСтроки);
	Иначе
		НомерСтроки = ВыбранноеЗначение.НомерСтроки;
		Если НомерСтроки > 0 Тогда
			НоваяСтрока = Элементы.Комментарии.ТекущиеДанные;
		Иначе
			НоваяСтрока = Объект.Комментарии.Добавить();
		КонецЕсли;
	КонецЕсли;

	НоваяСтрока.Комментарий = ВыбранноеЗначение.ВведенныйТекст;
	НоваяСтрока.Тэг 		= ВыбранноеЗначение.Тэг;
	НоваяСтрока.Якорь 		= ВыбранноеЗначение.Якорь;

	Если НомерСтроки = 0 Тогда
		Объект.Комментарии.Сдвинуть(ПолучитьКоличествоСтрокКомментариев() - 1, -1);
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти